define bar { translate(x: 0.5) rotate(0.25) line }
define fork(n) {
  translate(x: 0.5) rotate(0.25) {
    line
    linear [
      rotate(0.25) translate(y: -0.5) scale(0.5) line
      times n
    ]
  }
}

rune stroke(weight: 10) scale(0.75) {
  radial(offset: 0) [
    # N
    stack [
      line
      linear(scale: 0.25, min: 0.75) [
        bar
        bar
        bar
        fork(3)
      ]
    ]

    # NE
    stack [
      line
      linear(scale: 0.25, min: 0.5) [
        scale(0.5) {
          arc(-1/4, 1/4)
          fill("black") stack [
            { translate(-0.5) scale(0.1) circle }
            { translate( 0.5) scale(0.1) circle }
          ]
        }
        scale(0.75) translate(y: 1.6) arc(1/3, 2/3)
        fork(3)
      ]
    ]

    # E
    stack [
      line
      linear(scale: 0.25, min: 0.5) [
        group { translate(y: -0.25) scale(0.5) arc(-1/4, 1/4) }
        bar
        group { translate(y: 0.25) scale(0.5) arc(1/4, -1/4) }
        fork(3)
      ]
    ]

    # SE
    stack [
      line
      linear(scale: 0.25, min: 0.75) [
        bar
        bar
        bar
        fork(3)
      ]
    ]

    # S
    stack [
      line
      linear(scale: 0.25, min: 0.75) [
        group { 
          scale(x: 2) fork(3) 
          
          translate(x: -0.8) scale(0.25) line
          translate(x: -1, y: 0.8) scale(0.25) bar
          translate(x: -1, y: 1) scale(0.25) bar
          
          translate(x: 0.8) scale(0.25) line
          translate(x:  1, y: 0.8) scale(0.25) bar
          translate(x:  1, y: 1) scale(0.25) bar
        }
        group { scale(x: 1, y: 0.75) fork(3) }
        group { scale(0.5) bar }
      ]
    ]

    # SW
    stack [
      line
      linear(scale: 0.25, min: 0.5) [
        group { translate(y: -0.5) scale(0.5) arc(-1/4, 1/4) }
        bar
        group { scale(x: 1, y: 2) fork(3) }
        group { scale(0.5) bar }
        group { scale(0.5) bar }
        group {}
      ]
    ]

    # W
    stack [
      line
      translate(y: 0.9) scale(x: 0.5, y: 0.25) fork(5)
    ]

    # NW
    stack [
      line
      linear(scale: 0.25, min: 0.50) [
        scale(0.5) { translate(y: -0.25) arc(-1/4, 1/4) }
        group { scale(x: 2, y: 2) fork(3) }
        group { translate(y: -0.25) bar }
        group { translate(y: -0.25) scale(0.25) fill("none") circle }
      ]
    ]
  ]

  scale(0.15) circle
}